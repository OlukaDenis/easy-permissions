plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def keystorePropertiesFile = rootProject.file("azure-configs.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
    namespace 'com.interswitchug.iswpermissions'
    compileSdk 34

    defaultConfig {
        minSdk 21

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

afterEvaluate {
    publishing {
        publications {
            // Creates a publication named - release
            release(MavenPublication) {
                from components.findByName('release')
                groupId 'com.interswitchug'
                artifactId 'easypermissions'
                version '0.0.1'
            }
        }

        repositories {
            maven {
                name 'MobileDevOps'
                url keystoreProperties['repositoryUrl']
                credentials {
                    username keystoreProperties['userName']
                    password keystoreProperties['azureMavenAccessToken']
                }
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }
}